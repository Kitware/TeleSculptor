file(GLOB config_files *.conf *.pipe)
file(GLOB prop_files *.properties)
file(GLOB model_files *.gz)

# Ensure target directory exists
set(out_dir "${TELESCULPTOR_BINARY_DIR}/share/telesculptor/${TeleSculptor_VERSION}/config")
add_custom_command(
  OUTPUT "${out_dir}"
  COMMAND "${CMAKE_COMMAND}" -E make_directory "${out_dir}"
  )

# Set up meta-target
add_custom_target(telesculptor-data-config ALL DEPENDS "${out_dir}")

# Add targets to copy config files
foreach(file ${config_files} ${prop_files})
  get_filename_component(basename ${file} NAME_WE)
  get_filename_component(filename ${file} NAME)
  set(target ${basename})
  kwiver_configure_file(${target}
    "${CMAKE_CURRENT_SOURCE_DIR}/${filename}"
    "${out_dir}/${filename}"
    )
  add_dependencies(telesculptor-data-config configure-${target})
endforeach()

# Add targets to copy model files
foreach(file ${model_files})
  get_filename_component(filename ${file} NAME)
  configure_file(
    "${CMAKE_CURRENT_SOURCE_DIR}/${filename}"
    "${out_dir}/${filename}" COPYONLY
    )
endforeach()

# Install config and model files
kwiver_install(
  FILES ${config_files} ${model_files}
  COMPONENT runtime
  DESTINATION share/telesculptor/${TeleSculptor_VERSION}/config
  )

# Install Logger Properties
kwiver_install(
  FILES ${prop_files}
  COMPONENT runtime
  DESTINATION lib/kwiver
  )
